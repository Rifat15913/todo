apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Add kapt plugin for compiling data binding in kotlin
apply plugin: 'kotlin-kapt'

// To generate documentation
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId 'com.todo.app'
        // Does not support RTL (Right to left; i.e. Arabic), if (min SDK < 17)
        minSdkVersion 19
        targetSdkVersion 30

        // Enable MultiDex App; so that there will be no limitation (64k+) in method counting
        multiDexEnabled true

        // Version code and name should be changed for every release
        versionCode 1
        versionName '0.0.0.1'
        archivesBaseName = "ToDo-v$versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled = true
        }
        debug {
            multiDexEnabled = true
        }
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "documentation"

        // Do not output deprecated members
        skipDeprecated = true

        // Emit warnings about not documented members.
        reportUndocumented = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    productFlavors {
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "app_version_name", variant.versionName
    }

    bundle {
        language {
            enableSplit = false
        }
    }
}

dependencies {
    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.30'

    // Core with kotlin extension
    implementation 'androidx.core:core-ktx:1.3.2'

    // Add .jar libraries from external folder
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // MultiDex
    implementation 'androidx.multidex:multidex:2.0.1'

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Android support libraries
    // Appcompat
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // Design library
    implementation 'com.google.android.material:material:1.3.0'

    // Elegant number button
    implementation 'com.cepheuen.elegant-number-button:lib:1.0.3'

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'

    // Viewpager page indicator
    implementation 'me.relex:circleindicator:2.1.4'

    // CardView
    implementation "androidx.cardview:cardview:1.0.0"

    // Location API
    implementation 'com.google.android.gms:play-services-location:17.1.0'

    // Exif Interface (for image processing; i.e. rotate)
    implementation 'androidx.exifinterface:exifinterface:1.3.2'

    // Support vector drawable
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'

    // Animated vector drawable
    implementation "androidx.vectordrawable:vectordrawable-animated:1.1.0"

    // Constraint layout
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Pin Entry EditText for code verification
    implementation ('com.alimuzaffar.lib:pinentryedittext:2.0.6') {
        // Exclude is only needed if you already have
        // androidx.appcompat as a dependency in your app.
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }

    // Glide (Image loader)
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // uCrop (crops images)
    implementation 'com.github.yalantis:ucrop:2.2.2'

    // Stylish toast message
    implementation 'com.github.GrenderG:Toasty:1.4.2'

    // Reactive Extensions (Rx)
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'

    // Gson to convert from and to json
    implementation 'com.google.code.gson:gson:2.8.6'

    // RxBinding with Views
    // Platform bindings
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0"

    // Support-v4 bindings
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.2.0"

    // Appcompat-v7 bindings
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.2.0"

    // Design library bindings
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:2.2.0"

    // RecyclerView bindings
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.2.0"

    // Timber (Replacement of log)
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Text drawable
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    // Retrofit & OkHttp
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // Exo media player
    implementation 'com.google.android.exoplayer:exoplayer:2.10.4'

    // Android Media
    implementation 'androidx.media:media:1.2.1'

    // App data storage
    debugImplementation 'com.awesomedroidapps:inappstoragereader:1.0.2'

    // Testing
    testImplementation 'junit:junit:4.13.1'

    // JVM testing
    androidTestImplementation 'androidx.test:runner:1.3.0'
}

// Firebase
dependencies {
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:26.0.0')

    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
}